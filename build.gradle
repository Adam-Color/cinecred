plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.4.21'
}

group 'com.loadingbyte'
version '0.1.0-SNAPSHOT'

ext.javacppVersion = '1.5.4'
ext.ffmpegVersion = '4.3.1-1.5.4'
ext.architectures = ['linux-x86', 'linux-x86_64', 'macosx-x86_64', 'windows-x86', 'windows-x86_64']

repositories {
    mavenCentral()
}

dependencies {
    // Logging
    implementation 'org.slf4j:slf4j-simple:1.7.30'

    // Parsing
    implementation 'org.apache.commons:commons-csv:1.8'

    // Delivery
    implementation 'org.jfree:org.jfree.svg:4.1'
    implementation 'org.jfree:org.jfree.pdf:2.0'
    // FFmpeg
    implementation "org.bytedeco:javacpp:$javacppVersion"
    implementation "org.bytedeco:ffmpeg:$ffmpegVersion"
    for (arch in architectures) {
        implementation "org.bytedeco:javacpp:$javacppVersion:$arch"
        implementation "org.bytedeco:ffmpeg:$ffmpegVersion:$arch"
    }

    // UI
    implementation 'com.miglayout:miglayout-swing:5.2'
}

compileKotlin.kotlinOptions.jvmTarget = '11'
compileTestKotlin.kotlinOptions.jvmTarget = '11'

// Build fat JAR.
jar {
    manifest.attributes('Main-Class': 'com.loadingbyte.cinecred.MainKt')
    from {
        configurations.runtimeClasspath.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
}
